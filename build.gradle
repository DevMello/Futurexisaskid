buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {
            name = 'SpongePowered'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            url = 'https://plugins.gradle.org/m2'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

group =  'dev.futurex.futurex'//FIXME:
version =  '1.0.0'
archivesBaseName =  'FutureX'

compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

minecraft {
    version = '1.12.2-14.23.5.2768'
    runDir = 'run'
    mappings = 'snapshot_20180814'
    coreMod = 'dev.futurex.futurex.mixin.MixinLoader'//FIXME:
    makeObfSourceJar = false
}

repositories {
    jcenter()
    mavenCentral()

    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven {
        name = 'impactdevelopment-repo'
        url = "https://impactdevelopment.github.io/maven/"
    }
    maven {
        name = 'jitpack.io'
        url = "https://jitpack.io"
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:19.0.0'

    //Mixin
    compile('org.spongepowered:mixin:0.7.4-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }

    //Baritone
    compile 'com.github.cabaletta:baritone:1.2.14'
    compile 'cabaletta:baritone-api:1.2'

    //Minn RPC
    compile group: 'club.minnced', name: 'java-discord-rpc', version: '2.0.1'
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

shadowJar {
    dependencies {
        include(dependency('org.spongepowered:mixin'))
        include(dependency('cabaletta:baritone-api'))
        include(dependency('club.minnced:java-discord-rpc:2.0.1'))
    }

    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    classifier = ''
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, 'mixins.futurex.refmap.json'
}

jar.manifest.attributes(
        'MixinConfigs': 'mixins.futurex.json',
        'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
        'TweakOrder': 0,
        'FMLCorePluginContainsFMLMod': true,
        'FMLCorePlugin': 'dev.futurex.futurex.mixin.MixinLoader',//FIXME:
        'ForceLoadAsMod': true,
        'FMLAT': 'futurex_at.cfg'
)

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

build.dependsOn(shadowJar)